;;; centaur-tabs-vertical-test.el --- Tests for centaur-tabs-vertical -*- lexical-binding: t; -*-

(require 'ert)
(require 'cl-lib)

(let* ((this-file (or load-file-name buffer-file-name))
       (test-dir (file-name-directory this-file))
       (project-dir (directory-file-name (file-name-directory (directory-file-name test-dir))))
       (repo-dir (directory-file-name (file-name-directory project-dir))))
  (add-to-list 'load-path project-dir)
  (add-to-list 'load-path repo-dir))

(require 'centaur-tabs-vertical)

(ert-deftest centaur-tabs-vertical-pad ()
  (let ((result (centaur-tabs-vertical--pad "abc" 5 'default)))
    (should (= (string-width result) 5))
    (should (equal (substring result 0 3) "abc")))
  (let ((result (centaur-tabs-vertical--pad "abcdef" 3 'default)))
    (should (= (string-width result) 3))))

(ert-deftest centaur-tabs-vertical-tab-label ()
  (let ((centaur-tabs-tab-label-function (lambda (_tab) "X")))
    (should (equal (centaur-tabs-vertical--tab-label (cons (current-buffer) nil)) "X"))))

(ert-deftest centaur-tabs-vertical-render-tab-properties ()
  (with-temp-buffer
    (let* ((buf (current-buffer))
           (tab (cons buf 'dummy))
           (centaur-tabs-vertical-show-icons nil)
           (centaur-tabs-set-icons nil)
           (centaur-tabs-vertical-show-modified-marker nil)
           (centaur-tabs-set-modified-marker nil))
      (let ((left (centaur-tabs-vertical--render-tab tab tab 'left 10)))
        (should (= (string-width left) 12))
        (should (eq (get-text-property 0 'centaur-tabs-tab left) tab)))
      (let ((right (centaur-tabs-vertical--render-tab tab tab 'right 10)))
        (should (= (string-width right) 12))
        (should (eq (get-text-property 1 'centaur-tabs-tab right) tab))))))

(ert-deftest centaur-tabs-vertical-advice-line ()
  (let ((centaur-tabs-vertical-mode t))
    (should (null (centaur-tabs-vertical--advice-line (lambda (&rest _) 'ok)))))
  (let ((centaur-tabs-vertical-mode nil))
    (should (equal (centaur-tabs-vertical--advice-line (lambda (&rest _) 'ok)) 'ok))))

(ert-deftest centaur-tabs-vertical-interactive-toggle-commands ()
  (let (calls)
    (cl-letf (((symbol-function 'centaur-tabs-vertical-mode)
               (lambda (&optional arg) (push arg calls))))
      (centaur-tabs-vertical-enable)
      (centaur-tabs-vertical-disable))
    (should (equal (nreverse calls) '(1 0)))))

(ert-deftest centaur-tabs-vertical-close-button-properties ()
  (with-temp-buffer
    (let* ((buf (current-buffer))
           (tab (cons buf 'dummy))
           (centaur-tabs-set-close-button t)
           (centaur-tabs-set-left-close-button t)
           (centaur-tabs-close-button "x")
           (centaur-tabs-vertical-show-icons nil)
           (centaur-tabs-set-icons nil)
           (centaur-tabs-vertical-show-modified-marker nil)
           (centaur-tabs-set-modified-marker nil))
      (let* ((rendered (centaur-tabs-vertical--render-tab tab tab 'left 12))
             (first (string-match (regexp-quote centaur-tabs-close-button) rendered))
             (last (cl-search centaur-tabs-close-button rendered :from-end t))
             (left-map (and first (get-text-property first 'local-map rendered)))
             (right-map (and last (get-text-property last 'local-map rendered))))
        (should (eq left-map centaur-tabs-vertical-close-map))
        (should (eq right-map centaur-tabs-vertical-close-map))))))

(ert-deftest centaur-tabs-vertical-close-align-spacer-no-pad ()
  (with-temp-buffer
    (rename-buffer "abc" t)
    (let* ((buf (current-buffer))
           (tab (cons buf 'dummy))
           (centaur-tabs-set-close-button t)
           (centaur-tabs-set-left-close-button nil)
           (centaur-tabs-close-button "x")
           (centaur-tabs-vertical-show-icons nil)
           (centaur-tabs-set-icons nil)
           (centaur-tabs-vertical-show-modified-marker nil)
           (centaur-tabs-set-modified-marker nil)
           (rendered (centaur-tabs-vertical--render-tab tab tab 'left 20))
           (align-index (cl-loop for i from 0 below (length rendered)
                                 for disp = (get-text-property i 'display rendered)
                                 when (and (consp disp) (eq (car disp) 'space))
                                 do (cl-return i))))
      (should align-index)
      (should (not (eq (aref rendered (1- align-index)) ?\s))))))

(ert-deftest centaur-tabs-vertical-new-tab-button-properties ()
  (let* ((centaur-tabs-vertical-show-new-tab-button t)
         (centaur-tabs-new-tab-text " + ")
         (rendered (centaur-tabs-vertical--render-header "Group" 'left 20 t))
         (pos (string-match (regexp-quote centaur-tabs-new-tab-text) rendered)))
    (should pos)
    (should (eq (get-text-property pos 'local-map rendered)
                centaur-tabs-vertical-new-tab-map))))

(ert-deftest centaur-tabs-vertical-group-entry-properties ()
  (let* ((rendered (centaur-tabs-vertical--render-group-entry "GroupA" t 'left 20))
         (pos (string-match "GroupA" rendered)))
    (should pos)
    (should (equal (get-text-property pos 'centaur-tabs-vertical-group rendered) "GroupA"))
    (should (eq (get-text-property pos 'local-map rendered)
                centaur-tabs-vertical-group-map))))

(ert-deftest centaur-tabs-vertical-render-groups-list ()
  (cl-letf (((symbol-function 'centaur-tabs-get-groups)
             (lambda () '("A" "B")))
            ((symbol-function 'centaur-tabs-vertical--current-group-name)
             (lambda () "B")))
    (let ((lines (centaur-tabs-vertical--render-groups 'left 20)))
      (should (= (length lines) 2))
      (should (string-match "A" (nth 0 lines)))
      (should (string-match "B" (nth 1 lines))))))

(ert-deftest centaur-tabs-vertical-cleanup-force ()
  (let ((centaur-tabs-vertical-positions '(left))
        deleted
        killed)
    (cl-letf (((symbol-function 'get-buffer)
               (lambda (name) name))
              ((symbol-function 'get-buffer-window)
               (lambda (_buf) 'dummy-window))
              ((symbol-function 'window-live-p)
               (lambda (_win) t))
              ((symbol-function 'buffer-live-p)
               (lambda (_buf) t))
              ((symbol-function 'delete-window)
               (lambda (win) (push win deleted)))
              ((symbol-function 'kill-buffer)
               (lambda (buf) (push buf killed))))
      (centaur-tabs-vertical--cleanup-windows t)
      (should (= (length deleted) 2))
      (should (= (length killed) 2))
      (should (member (centaur-tabs-vertical--buffer-name 'left) killed))
      (should (member (centaur-tabs-vertical--buffer-name 'right) killed)))))

(ert-deftest centaur-tabs-vertical-close-align-when-truncated ()
  (with-temp-buffer
    (rename-buffer "very-long-buffer-name-that-should-truncate" t)
    (let* ((buf (current-buffer))
           (tab (cons buf 'dummy))
           (centaur-tabs-set-close-button t)
           (centaur-tabs-set-left-close-button nil)
           (centaur-tabs-close-button "x")
           (centaur-tabs-vertical-show-icons nil)
           (centaur-tabs-set-icons nil)
           (centaur-tabs-vertical-show-modified-marker nil)
           (centaur-tabs-set-modified-marker nil)
           (rendered (centaur-tabs-vertical--render-tab tab tab 'left 12))
           (align-index (cl-loop for i from 0 below (length rendered)
                                 for disp = (get-text-property i 'display rendered)
                                 when (and (consp disp) (eq (car disp) 'space))
                                 do (cl-return i)))
           (close-index (cl-search centaur-tabs-close-button rendered :from-end t)))
      (should align-index)
      (should close-index)
      (should (< align-index close-index))
      (should (not (string-match-p (regexp-quote (buffer-name buf)) rendered))))))
